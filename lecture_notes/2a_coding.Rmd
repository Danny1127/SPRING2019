---
title: "General Programming and Julia Specifics"
subtitle: "<html><div style='float:left'></div><hr color='#459DE0' size=5px width=1100px></html>"
author: Ivan Rudik
date: AEM 7130
output:
  xaringan::moon_reader:
    css: [default, metropolis, metropolis-fonts] 
    lib_dir: libs
    nature:
      highlightStyle: julia
      highlightLines: true
      countIncrementalSlides: false
      ratio: '16:9'

---

# Software and stuff

Necessary things to do:

- Install [Julia](https://julialang.org/downloads/)

--

These slides are based on QuantEcon, Julia documentation, etc

---

# First things first

Why am I pushing you to learn Julia?

1. It's a high-level language, much easier to use than C++, Fortran, etc
2. It delivers speeds in the ballpark of C++ and Fortran even with the super old 0.2 version

<div align="center">
  <img src="figures/julia_speed.png" height=450>
</div>

---

# Intro to programming

### Programming $\equiv$ writing a set of instructions

1. There are hard and fast rules you can't break if you want it to work
2. There are elements of style (e.g. Strunk and White) that make for clearer and more efficient code


--

If you will be doing computational work there are:

1. Language-independent coding basics you should know
  - Arrays are stored in memory in particular ways  
--

2. Language-independent best practices you should use
  - Indent to convey program structure (or function in Python)   
--

3. Language-dependent idiosyncracies that matter for function, speed, memory allocation, etc
  - MATLAB: vectorize, vectorize, vectorize
  - Julia: loops forever

---
# Intro to programming

### Learning these early will:

1. Make coding a lot easier
--

2. Reduce total programmer time
--

3. Reduce total computer time
--

4. Make your code understandable by someone else or your future self
--

5. Make your code flexible

---

# A broad view of programming

Your goal is to make a **program**

A program is made of different components and sub-components

--
 
The most basic component is a **statement**, more commonly called a **line of code**

--

Here's pseudoprogram:  
```julia
deck = ["4 of hearts", "King of clubs", "Ace of spades"]
shuffled_deck = shuffle(deck)
first_card = shuffled_deck[1]
println("The first drawn card was " * shuffled_deck ".")
```

This program is real simple:

---

# A broad view of programming

Your goal is to make a **program**

A program is made of different components and sub-components
 
The most basic component is a **statement**, more commonly called a **line of code**

Here's pseudoprogram:  
```julia
*deck = ["4 of hearts", "King of clubs", "Ace of spades"]
shuffled_deck = shuffle(deck)
first_card = shuffled_deck[1]
println("The first drawn card was " * shuffled_deck ".")
```

This program is real simple:

1. Create a deck of cards

---

# A broad view of programming

Your goal is to make a **program**

A program is made of different components and sub-components
 
The most basic component is a **statement**, more commonly called a **line of code**

Here's pseudoprogram:  
```julia
deck = ["4 of hearts", "King of clubs", "Ace of spades"]
*shuffled_deck = shuffle(deck)
first_card = shuffled_deck[1]
println("The first drawn card was " * shuffled_deck ".")
```

This program is real simple:

1. Create a deck of cards
2. Shuffle the deck

---

# A broad view of programming

Your goal is to make a **program**

A program is made of different components and sub-components
 
The most basic component is a **statement**, more commonly called a **line of code**

Here's pseudoprogram:  
```julia
deck = ["4 of hearts", "King of clubs", "Ace of spades"]
shuffled_deck = shuffle(deck)
*first_card = shuffled_deck[1]
println("The first drawn card was " * shuffled_deck ".")
```

This program is real simple:

1. Create a deck of cards
2. Shuffle the deck
3. Draw the top card

---

# A broad view of programming

Your goal is to make a **program**

A program is made of different components and sub-components
 
The most basic component is a **statement**, more commonly called a **line of code**

Here's pseudoprogram:  
```julia
deck = ["4 of hearts", "King of clubs", "Ace of spades"]
shuffled_deck = shuffle(deck)
first_card = shuffled_deck[1]
*println("The first drawn card was " * shuffled_deck ".")
```

This program is real simple:

1. Create a deck of cards
2. Shuffle the deck
3. Draw the top card
4. Print it

---

# A broad view of programming  
```julia
deck = ["4 of hearts", "King of clubs", "Ace of spades"]
shuffled_deck = shuffle(deck)
first_card = shuffled_deck[1]
println("The first drawn card was " * shuffled_deck ".")
```

What are the parentheses and why are they different from square brackets?

How does shuffle work? 

What’s println? 

It’s important to know that a good program has understandable code

---

# Julia specifics

We will discuss coding in the context of Julia but a lot of this ports to Python, MATLAB, etc

To do:

1. Types
2. Operators
3. Scope
4. Generic functions
5. Multiple dispatch

---

# Types

All languages have some kind of **data types** like integers or arrays

The first type you will often use is a boolean (`Bool`) variable that takes on a value of `true` or `false`:
```{julia}
x = true
typeof(x)
```
Note that lines starting with `##` are code output

--

We can save the boolean value of actual statements in variables this way:
```{julia}
@show y = 1 > 2
```

`@show` is a Julia macro for showing the operation

---

# Numbers

Two other data types you will use frequently are integers
```{julia}
typeof(1)
```

and floating point numbers
```{julia}
typeof(1.0)
```
Recall from last week the 64 means 64 bits of storage for the number, which is probably the default on your machine

--

But you can always instantiate alternative floating point number types

```{julia}
converted_int = convert(Float32, 1.0);
typeof(converted_int)
```

---

# Numbers

### Math works like you would expect:
```{julia}
a = 2; b = 1.0;
a * b
```
--
```{julia}
a^2
```
--
```{julia}
2a - 4b
```
--
```{julia}
@show 4a + 3b^2
```
--
You dont need `*` inbetween numeric literals (numbers) and variables

---

# Strings

Strings store sequences of characters

You implement them with double quotations:

```{julia}
x = "Hello World!";
typeof(x)
```

Note that `;` suppresses output for that line of code but is unnecessary

--

It's easy to work with strings, use `$` to interpolate a variable/expression
```{julia}
x = 10; y = 20; println("x + y =  $(x+y).")
```

--

Use `*` to concatenate strings
```{julia}
a = "Aww"; b = "Yeah!!!"; println(a * " " * b)
```

--

You probably won't use strings too often unless you're working with data or printing output

---

# Containers

Containers are types that store collections of data

--

The most basic container is the `Array` which is denoted by square brackets

--


```{julia}
a1 = [1 2; 3 4]; typeof(a1)
```
`a1` is a two-dimensional `Array` of `Int64`s

--

Arrays are **mutable** which means you can change their values

--


```{julia}
a1[1,1] = 5; a1
```

You reference elements in a container with square brackets

---

# Containers

An alternative to the `Array` is the `Tuple` which is denoted by parentheses

--

```{julia}
a2 = (1, 2, 3, 4); typeof(a2)
```
`a2` is a `Tuple` of 4 `Int64`s, tuples have no dimension

--

Tuples are **immutable** which means you **can't** change their values
```{julia}
a2[1,1] = 5;
```

--


```{julia}
a2
```

---

# Containers

Tuples don't need parentheses (but it's probably best practice for clarity)
```{julia}
a3 = 5, 6; typeof(a3)
```

--

And they can be **unpacked** (see [`NamedTuple`](https://docs.julialang.org/en/v1/manual/types/index.html#Parametric-Types-1) for an alternative and more efficient container)

--

```{julia}
a3_x, a3_y = a3;
a3_x
a3_y
```

--

This is basically how functions return output when you call them

---

# Containers

A `Dictionary` is the last main container type, they are arrays but are indexed by keys (names) instead of numbers

--

```{julia}
d1 = Dict("class" => "AEM7130", "grade" => 97); 
typeof(d1)
```

--

`d1` is a dictionary where the key are strings and the values are any kind of type

---

# Containers

Reference specific values you want in the dictionary by referencing the key

--

```{julia}
d1["class"]
d1["grade"]
```

--

If you just want all the keys or all the values you can use the base functions

--

```{julia}
keys_d1 = keys(d1)
values_d1 = values(d1)
```

---

# Iterating

As in other languages we have loops at our disposal:

`for` loops iterate over containers
```{julia}
for count in 1:10
  random_number = rand()
  if random_number > 0.5
    println("We drew a $random_number.")
  end
end
```

---
  
# Iterating

`while` loops iterate until a logical expression is false 
```{julia}
while rand() > 0.5
  random_number = rand()
  if random_number > 0.5
    println("We drew a $random_number.")
  end
end
```

---

# Iterating

An `Iterable` is something you can loop over, like arrays

--

```{julia}
actions = ["codes well", "skips class"];
for action in actions
    println("Charlie $action")
end
```

--

But there's a type that's a subset of iterables: `Iterator`

--

These include things like the dictionary keys:
```{julia}
for key in keys_d1
  println(d1[key])
end
```

---

# Iterating

Iterating on iterators is more memory efficient than iterating on arrays

--

Here's a **very** simple example (the `global` isn't great style but ignore that; advanced tip: fix it with a wrapper function):

--

```{julia}
j = 1;
@time for i = [1, 2, 3, 4, 5, 6]
  global j *= i
end
```

--

```{julia}
m = 1;
@time for i = 1:6
  global m *= i
end
```

--

`@time` is a macro that shows you the elasped time, memory allocation, and number of allocations (also garbage collection)

--

Minimizing allocations and garbage collection will make your code faster (see `@profile`)

---

# Neat looping

The nice thing about Julia vs MATLAB is your loops can be much neater since you don't need to index if you just want the container elements

--

```{julia}
f(x) = x^2;
x_values = 0:20:100;
for x in x_values
  println(f(x))
end
```

--

The loop directly assigns the elements of `x_values` to `x` instead of having to do something clumsy like `x_values[i]`

--

`0:20:100` creates something called a `StepRange` which starts at `0`, steps up by `20` and ends at `100`


---

# Neat looping

You can also pull out an index and the element value by enumerating

```{julia}
f(x) = x^2;
x_values = 0:20:100;
for (index, x) in enumerate(x_values)
  println("f(x) at value $index is $(f(x)).")
end
```

`enumerate` basically assigns an index vector

---

# Neat looping

There is also a lot of Python-esque functionality

--

For example: `zip` lets you loop over multiple different iterables at once

--

```{julia}
last_name = ("Lincoln", "Bond", "Walras");
first_name = ("Abraham", "James", "Leon");

for (first_idx, last_idx) in zip(first_name, last_name)
  println("The name's $last_idx, $first_idx $last_idx.")
end
```

---

# Neat looping

Nested loops can also be made very neatly

--

```{julia}
for x in 1:3, y in 3:-1:1
  println(y-x)
end
```

--

The first loop is the inner loop, the second loop is the outer loop

---

# Comprehensions: the neatest looping

Comprehensions are super nice ways to use iterables that make your code cleaner and more compact

--

```{julia}
squared = [y^2 for y in 1:2:11]
```

--

This created a 1-dimension `Array` using one line

---

# Comprehensions: the neatest looping

We can also use nested loops for comprehensions

--

```{julia}
squared_2 = [(y+z)^2 for y in 1:2:11, z in 1:6]
```

--

This created a 2-dimensional `Array`

--

Use this (and the compact nested loop) sparingly since it's hard to follow

---

# Dot syntax: vectorization

Vectorizing operations (e.g. applying it to a whole array or vector at once) is easy in Julia, just use dot syntax like you would in MATLAB, etc

--

```{julia}
g(x) = x^2;
squared_2 = g.(1:2:11)
```

--

This is actually called **broadcasting**

--

When broadcasting, you might want to consider **pre-allocating** arrays

---
# Dot syntax: vectorization

Not pre-allocated:

```{julia}
h(y,z) = y^2 + sin(z);   # function to evaluate
y = 1:2:1e6+1;           # input y
z = rand(length(y));     # input z
out_1 = h.(y,z);         # pre-evaluate h.(y,z)
@time out_1 = h.(y,z)    # evaluate h.(y,z) and time
```

---
# Dot syntax: vectorization

Pre-allocated, clumsy dot syntax where `.=` assigns elements in a vectorized fashion:

```{julia}
out_2 = similar(y);
out_2 .= h.(y,z);
@time out_2 .= h.(y,z)
```

---
# Dot syntax: vectorization

Pre-allocated, galaxy brain dot syntax:

```{julia}
out_3 = similar(y);
@. out_3 = h(y,z);
@time @. out_3 = h(y,z)
```

---
# Logical operators

Logical operators work like every other language 

--

- `==` tests for equality

--

```{julia}
1 == 1
```

--

- `!=` tests for inequality

--

```{julia}
2 != 2
```

--

- You can also test for approximate equality with $\approx$ (type `\approx<TAB>`)

--

```{julia}
1.00000001 ≈ 1
```

---
# Scope

**Scope** can be a frustrating concept if you haven't used a similarly scoped language before (and why I used that `global` indicator before)

--

The scope of a variable name determines when it is valid to refer to it

--

If you want to dive into the details: the type of scoping in Julia is called **lexical scoping**

--

Different scopes can have the same name, i.e. `saving_rate`, but be assigned to different variables

--

Let's walk through some simple examples to see how it works

--

First, functions have their own local scope

--

```{julia}
ff(xx) = xx^2;
yy = 5;
ff(yy)
```

`xx` isn't bound to any values outside the function `ff`

---
# Scope

This allows us to do things like:

```{julia}
xx = 10;
fff(xx) = xx^2;
fff(5)
```

Although `xx` was declared equal to 10, the function still evaluated at 5

--

This is all kind of obvious so far

---

# Scope

But, this type of scoping also has (initially) counterintuitive results like:

```{julia}
zz = 0;
for ii = 1:10
  zz = ii
end
println("zz = $zz")
```

--

What happened?

--

The `zz` outside the for loop has a different scope, the **global scope**, than the `zz` inside it

--

The `zz` inside the for loop has a scope local to the loop

--

Since the outside `zz` has global scope the loop can't change it without the `global` indicator like we saw earlier 

---

# Scope

Generally you want to avoid `global` because it can cause conflicts, slowness, etc, but you can use them if you want something to have global scope

```{julia}
zz = 0;
for ii = 1:10
  global zz
  zz = ii
end
println("zz = $zz")
```

---

# Scope

Local scope kicks in whenever you have a new block keyword (i.e. you indented something) except for `if`

Global variables inside a local scope are inherted for reading, not writing

```{julia}
x, y = 1, 2;
function foo()
  x = 2        # assignment introduces a new local
  return x + y # y refers to the global
end;
foo()
x
```

`foo` did not alter the value of `x` defined outside its local scope

---

# Scope

Important piece: nested functions can modify their parent scope's *local* variables

--

```{julia}
x, y = 1, 2; # set globals

function f_outer()
  x = 2                # introduces a new local
  function f_inner()
    x = 10             # modifies the parent's x
    return x + y       # y is global
  end
  return f_inner() + x # 12 + 10 (x is modified in call of bar())
end;
f_outer()
x, y                   # verify that global x and y are unchanged
```

--

If `f_inner` was not nested and was in the global scope we'd get `14` not `22`, this is also a way to handle the issue with loops editing variables not created in their local scope

---

# Scope

We can fix looping issues with global scope by using a wrapper function that doesn't do anything but change the parent scope so it is not global

```{julia}
function wrapper()
  zzz = 0;
  for iii = 1:10
    zzz = iii
  end
  println("zzz = $zzz") 
end
wrapper()
```

---

# Closures

These inner functions we've been looking at are called **closures**

When a function `f` is parsed in Julia, it looks to see if any of the variables have been previously defined in the current scope

```{julia}
a = 0.2;
f(x) = a * x^2;    # refers to the `a` in the outer scope
f(1)               # univariate function
```

--

```{julia}
function g(a)
    f(x) = a * x^2; # refers to the `a` passed in the function
    f(1);           # univariate function
end
g(0.2)
```

--

In both of these examples `f` is a closure designed to **capture** a variable from an outer scope

---

# Closures

Here's a complicated example that actually returns a closure (a function!) itself:

```{julia}
x = 0;
function toplevel(y)
  println("x = ", x, " is a global variable")
  println("y = ", y, " is a parameter")
  z = 2
  println("z = ", z, " is a local variable")
  
  function closure(v)
    println("v = ", v, " is a parameter")
    w = 3
    println("w = ", w, " is a local variable")
    println("x = ", x, " is a global variable")
    println("y = ", y, " is a closed variable (a parameter of the outer function)")
    println("z = ", z, " is a closed variable (a local of the outer function)")
  end;
  return closure
end;
```

---

# Closures

Here's a complicated example:

```{julia}
c_func = toplevel(10)
c_func(20)
```

The returned closure still has access to the outer function's local scope!

---

# Closures

Closures are what allow programmers to write packages that do functions of functions, e.g. taking expectations
```{julia}
using Expectations, Distributions

@show d = Exponential(2.0)

f(x) = x^2
@show expectation(f, d)  # E(f(x))
```

---

# Generic functions

If you go down a computational route with research a goal you should have in mind is to write **generic functions**

--

These are functions that are flexible (e.g. can deal with someone using an `Int` instead of a `Float`) and have high performance (e.g. comparable speed to C)

--

Functions are made generic by paying attention to types and making sure types are **stable**

--

**Type stability:** Given an input, operations on that input should maintain the type so Julia **knows** what its type will be throughout

--

This allows it to compile type-specialized versions of the functions, which will yield higher performance

--

The question you might have is: Type stability sounds like mandating types, so how do we make it flexible?

--

We'll see next

---

# These two functions look the same, but are they?

```{julia}
function t1(n)
  s = 0
  t = 1
  for i in 1:n
     s += s/i
     t = div(t, i)
  end
  return t
end
```
```{julia}
function t2(n)
  s  = 0.0
  t = 1
  for i in 1:n
     s += s/i
     t = div(t, i)
  end
  return t
end
```

---

# No! t1 is not type stable

--

`t1` starts with `s` as an `Int64` but then we have `s += s/i` which will mean it must hold a `Float64`

--

It must be converted to `Float` so it is not type stable

--

We can see this when calling the macro `@code_warntype` where it reports `t1` must at some point handle a variable that is `Union{Float64,Int64}`, either `Float64` or `Int64`

Julia now can't assume `s`'s type and produce pure integer or floating point code $\rightarrow$ performance degradation

<div align="center">
  <img src="figures/code_warntype.png" height=350>
</div>

---

# Concrete vs abstract types

A **concrete type** is one that can be instantiated (`Float64` `Bool` `Int32`)

--

An **abstract type** cannot (`Real`, `Number`, `Any`)

--

Abstract types are for organizing the types

You can check where types are in the hierarchy

```{julia}
@show Float64 <: Real
@show Array <: Real
@show Number <: Any
```

---

# Concrete vs abstract types

You can see the type hierarchy with the supertypes and subtypes commands

```{julia}
using Base: show_supertypes
show_supertypes(Float64)
```

---

# Creating new types

We can actually create new composite types using `struct`

--

```{julia}
struct FoobarNoType # This will be immutable by default
  a
  b
  c
end
```

--

This creates a new type called `FoobarNoType`, and we can generate a variable of this type using its **constructor** which will have the same name

--

```{julia}
newfoo = FoobarNoType(1.3, 2, "plzzz");
typeof(newfoo)
newfoo.a
```

--

### You should always declare types for the fields of a new composite type

---

# Creating new types

You can declare types with the double colon


```{julia}
struct FoobarType # This will be immutable by default
  a::Float64
  b::Int
  c::String
end
```

--

```{julia}
newfoo_typed = FoobarType(1.3, 2, "plzzz");
typeof(newfoo_typed)
newfoo.a
```

This lets the compiler generate efficient code because it knows the types of the fields when you construct a `FoobarType`

Declaring abstract types isn't good enough, you need to declare concrete types

--

Or do we?

---

# Parametric types are what help deliver flexibility

We can create types that hold different types of fields by declaring subsets of abstract types

```{julia}
struct FooParam{t1 <: Real, t2 <: Real, t3 <: AbstractArray{<:Real}}
  a::t1
  b::t2
  c::t3 
end
newfoo_para = FooParam(1.0, 7, [1., 4., 6.])
```

--

The curly brackets declare all the different type subsets we will use in `FooParam`

--

This actually delivers high performance code!

---

# Smart use of existing Julia functions delivers flexibility

We want to make sure types are stable but code is flexible

Ex: if want to preallocate an array to store data, how do we know how to declare it's type?

--

We don't need to

--

```{julia}
using LinearAlgebra               # necessary for I
function sametypes(x)
  y = similar(x)                  # creates an array that is `similar` to x, use this for preallocating
  z = I                           # creates a scalable identity matrix
  q = ones(eltype(x), length(x))  # one is a type generic array of ones, fill creates the array of length(x)
  y .= z * x + q
  return y
end

x = [5.5, 7.0, 3.1];
y = [7, 8, 9];
```

--

We did not declare any types but the function is type stable

---

# Smart use of existing Julia functions delivers flexibility

```{julia} 
sametypes(x)
sametypes(y)
```

--

There's a lot of other functions out there that help with writing flexible, type stable code

---

# Multiple dispatch

Neat thing about Julia: the same function name can perform different operations depending on the underlying type of the inputs

A function specifies different **methods**, each of which operates on a specific set of types

`/` has 103 different methods depending on the input types, these are 103 specialized sets of codes

```{julia}
methods(/)
```  


---
  
# Coding practices etc

See [JuliaPraxis](https://github.com/JuliaPraxis) for best practices for naming, spacing, comments, etc
